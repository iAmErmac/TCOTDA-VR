
Class RevolverBlood2 : WeaponZ replaces PistolBloodPickup
{
	Default
	{
	obituary "%o was tickled by %k's pea shooter.";
	Inventory.PickupMessage "You got the anaconda!";
	attacksound "weapons/pistol";
	Inventory.PickupSound "misc/w_pkup";
	weapon.selectionorder 190;
	weapon.kickback 100;
	weapon.ammotype "AClip";
	weapon.ammouse 1;
	weapon.ammogive 20;
	+WEAPON.WIMPY_WEAPON;
	}
	
	states
	{
	Ready:
		PISG A 0 A_ReplaceSprite("cotd_weapon_mode", "PISH");
		#### A 1 A_WeaponReady;
		loop;
	Deselect:
		#### A 1 A_Lower(160);
		loop;
	Select:
		PISG A 0 A_ReplaceSprite("cotd_weapon_mode", "PISH");
		#### A 1 A_Raise(160);
		loop;
	Fire:
		#### B 3;
		#### B 0 A_FireBullets (5.6, 0, 1, 9, "BulletPuff"); //yes, 10 power is not much considering average enemy health!!
		#### C 2 A_GunFlash; //besides, this thing doesn't have fast firespeed
		#### DE 2;
		#### A 10;
		#### E 8 A_ReFire;
		goto Ready;
	Flash:
		TNT1 A 0 A_JumpIf(invoker.showTracer == false, 2);
		TNT1 A 0 A_FireProjectile("Bullettracer", 0, 0, 0, 0, FPF_NOAUTOAIM, 0);
		TNT1 A 0 A_JumpIf(invoker.showFlash == false, 3);
		PISF A 2 bright A_Light1;
		PISF A 0 bright A_Light0;
		stop;
		TNT1 A 2 bright A_Light1;
		TNT1 A 0 bright A_Light0;
		stop;
	Spawn:
		PIPP A -1;
		Stop;
	Dummy:
		PISH ABCDEFGH 1;
		Stop;
	}
}

CLass PistolBloodPickup2 : CustomInventory //19999
{
	Default
	{
	Inventory.PickupMessage "You got the anaconda!";
	Inventory.PickupSound "misc/w_pkup";
	}
	
	states
	{
	Spawn:
		PIPP A -1;
		stop;
	Pickup:
		PIPP A 0 A_JumpIfInventory("RevolverBlood2",1,2);
		PIPP A 0 A_GiveInventory("RevolverBlood2",1);
		stop;
		PIPP A 0 A_GiveInventory("AClip",20);
		stop;
	}
}