
Class Sawedoff2 : WeaponZ //27010
{
	Default
	{
	//SpawnID 33;
	OBITUARY "%o chewed on %k's Sawed off.";
	Radius 20;
	Height 16;
	ATTACKSOUND "dsSAWFIR";
	Inventory.PickupMessage "You got the Sawed off!";
	Inventory.PickupSound "misc/w_pkup";
	Weapon.SelectionOrder 130;
	Weapon.Kickback 100;
	Weapon.AmmoType "AutoShotgunClip";
	Weapon.AmmoUse 1;
	Weapon.AmmoType2 "Shell";
	Weapon.AmmoUse2 2;
	Weapon.AmmoGive 2;
	Weapon.AmmoGive2 8;
	+WEAPON.AMMO_OPTIONAL;
	+WEAPON.ALT_AMMO_OPTIONAL;
	}
	
	action void A_SwitchToOtherWeapon()
	{
		let pmo = invoker.owner.player.mo;
		
		if(pmo.findinventory("Thompson2", 1) && pmo.countinv("AClip") > 0)
		{
			A_SelectWeapon("Thompson2");
		}
		else if(pmo.findinventory("NapalmLauncher2", 1) && pmo.countinv("NapalmAmmo") > 0)
		{
			A_SelectWeapon("NapalmLauncher2");
		}
		else if(pmo.findinventory("RevolverBlood2", 1) && pmo.countinv("AClip") > 0)
		{
			A_SelectWeapon("RevolverBlood2");
		}
		else if(pmo.findinventory("Pitchfork2", 1))
		{
			A_SelectWeapon("Pitchfork2");
		}
		else
		{
			A_SelectWeapon("Fist2");
		}
	}
	
	states
	{
	Ready:
		TNT1 A 0;
		SHTG Z 0 A_ReplaceSprite("cotd_weapon_mode", "STGH");
		#### Z 1 A_WeaponReady(WRF_ALLOWRELOAD);
		Loop;
	DeSelect:
		#### Z 1 A_Lower(160);
		Loop;
	Select:
		SHTG Z 0 A_ReplaceSprite("cotd_weapon_mode", "STGH");
		#### Z 1 A_Raise(160);
		Loop;
	Fire:
		#### B 0 A_JumpIfInventory("AutoShotgunClip",1,1); //Fire if not empty
		Goto Reload;
		#### A 0 A_JumpIfInventory("AutoShotgunClip",2,1);
		Goto FireRIghtBArrel;
		#### BC 1;
		#### C 0 A_Gunflash;
		#### C 0 A_PlaySound ("weapons/sshotf");
		#### C 0 A_FireBullets (5.6, 0, 7, 5, "BulletPuff");
		#### DE 1;
		Goto FireDOne;
	FireRIghtBArrel:
		#### EF 1;
		#### F 0 A_Gunflash("RightFlash");
		#### F 0 A_PlaySound ("weapons/sshotf");
		#### F 0 A_FireBullets (5.6, 0, 7, 5, "BulletPuff");
		#### GH 1;
	FireDOne:
		#### # 7;
		#### # 0 A_JumpIfInventory("AutoShotgunClip",1,3);
		#### A 0 A_JumpIfInventory("Shell",2,2);
		#### A 0 A_SwitchToOtherWeapon();
		Goto Reload;
		#### A 0 A_Refire;
		Goto Ready;
	Reload:
		#### A 0 A_JumpIfInventory("Shell",2,2);
		#### A 0 A_SwitchToOtherWeapon();
		Goto DeSelect;
		SHTR A 0 A_ReplaceSprite("cotd_weapon_mode", "STRH");
		#### ABCDEF 2;
		#### Z 12;
		#### GHIJ 2;
		#### J 0 A_PlaySound ("weapons/sshotc");
		#### KLMNO 2;
		#### O 0 A_TakeInventory("Shell",2);
		#### O 0 A_GiveInventory("AutoShotgunClip",2);
		#### O 7 A_Refire;
		Goto Ready;
	AltFire:
		#### A 0 A_JumpIfInventory("AutoShotgunClip",2,1); //Has the player fired once, if so, reload
		Goto Reload;
		#### IJ 1;
		#### J 0 A_PlaySound ("weapons/sshotf");
		#### J 0 A_Gunflash;
		#### J 0 A_FireBullets (11.2, 7.1, 20, 5, "BulletPuff",0);
		#### KL 1;
		#### L 0 A_TakeInventory("AutoShotgunClip",2); //For constancy's sake, drain the altfire from the same well as the primary fire
		#### A 0 A_JumpIfInventory("Shell",2,2);
		#### A 0 A_SwitchToOtherWeapon(); // Out of ammo, so autoswitch to best available weapon
		Goto DeSelect;
		#### A 5;
		#### A 0 A_Refire;
		Goto Ready;
	Flash:
		TNT1 A 0 A_JumpIf(invoker.showTracer == false, 4);
		TNT1 AAA 0 A_FireProjectile("Bullettracer", frandom(-3, 3), 0, 0, 0, FPF_NOAUTOAIM, 0);
		TNT1 A 0 A_JumpIf(invoker.showFlash == false, "NoFlash");
		SHTF A 2 BRIGHT A_Light1;
		SHTF B 1 BRIGHT A_Light2;
		SHTF C 1 BRIGHT A_Light0;
		Goto LightDone;
	RightFlash:
		TNT1 A 0 A_JumpIf(invoker.showTracer == false, 4);
		TNT1 AAA 0 A_FireProjectile("Bullettracer", frandom(-3, 3), 0, 0, 0, FPF_NOAUTOAIM, 0);
		TNT1 A 0 A_JumpIf(invoker.showFlash == false, "NoFlash");
		SHTF D 2 BRIGHT A_Light1;
		SHTF E 1 BRIGHT A_Light2;
		SHTF F 1 BRIGHT A_Light0;
		Goto LightDone;
	AltFlash:
		TNT1 A 0 A_JumpIf(invoker.showTracer == false, 7);
		TNT1 AAAAAA 0 A_FireProjectile("Bullettracer", frandom(-3, 3), 0, 0, 0, FPF_NOAUTOAIM, 0);
		TNT1 A 0 A_JumpIf(invoker.showFlash == false, "NoFlash");
		SHTF G 2 BRIGHT A_Light1;
		SHTF H 1 BRIGHT A_Light2;
		SHTF I 1 BRIGHT A_Light0;
		Goto LightDone;
	NoFlash:
		TNT1 A 3 BRIGHT A_Light1;
		TNT1 A 2 BRIGHT A_Light2;
		TNT1 A 1 BRIGHT A_Light0;
		Goto LightDone;
	Spawn:
		SHOT A -1;
		Stop;
	Dummy:
		STGH ABCDEFGHIJKL 1;
		STRH ABCDEFGHIJKLMNO 1;
		Stop;
	}
} 